ROW_OFFSET	.alias RAM_BASE + 0;	// space to mark first address of up to 61 rows
CUR_OLDX	.alias RAM_BASE + 60;
CUR_OLDY	.alias RAM_BASE + 61;
CURSOR_X	.alias RAM_BASE + 62;
CURSOR_Y	.alias RAM_BASE + 63;

			.org $;

// Initialize display
VGA_Init:	JSR CLC;
			LDAI VGA_COLS;
			PUSHA;
			LDAI VGA_BASE;	// base address is the start of video RAM
			LDXI VGA_ROWS;
			LDYI ROW_OFFSET;
			STA, M[Y];
RowOffLoop:	LDA, M[Y];		// previous row 
			INY, ADC, M[S];	// add number of columns, point to current row
			DEX, STA, M[Y];
			IF_X_NE_THEN RowOffLoop;
			POPA;
			CLRXY;
			JSR SetOldCrsXY;	// set "old" cursor (new will be set by CLS)
			LDAI CLS;
			// continue with "clear screen"
			
// Display ASCII in A at CURSOR_X, CURSOR_Y
VGA_OutChr: PUSHX;
			PUSHY;
			ANDI 0x00FF;	// care only about the lower 8-bit
			PUSHA;

			XORI HOME;
			IF_A_EQ_THEN VGA_HOME;
			
			LDA, M[S];
			XORI CLS;
			IF_A_EQ_THEN VGA_CLS;

			LDA, M[S];
			XORI BEL;
			IF_A_EQ_THEN VGA_Ignore;

			LDA, M[S];
			XORI ETX;
			IF_A_EQ_THEN VGA_Ignore;
			
			JSR GetCursorXY;	// get cursor row, col from RAM

			LDA, M[S];
			XORI CR;		
			IF_A_EQ_THEN VGA_CR;

			LDA, M[S];
			XORI LF;		
			IF_A_EQ_THEN VGA_LF;

			LDA, M[S];
			XORI BS;
			IF_A_EQ_THEN VGA_BS;
			
//			default "printable" character output
			POPA;
			
VGA_Print:	PUSHY;
			JSR GetRowBase;
			STA, M[Y+X];
			INX, POPY;
			CPXI VGA_COLS;
			IF_X_GE_THEN NextRow;
SetCursorXY:PUSHY;
			LDYI CURSOR_X;
			STX, M[Y];	// M[CURSOR_X] <= X
			LDXI CURSOR_Y;
			POPY;
			STY, M[X];	// M[CURSOR_Y] <= Y
// main exit restores Y and X
			POPY;
			POPX;
			RTS;

VGA_Ignore: POPA;
			POPY;
			POPX;
			RTS;
			
SetOldCrsXY:PUSHY;
			LDYI CUR_OLDX;
			STX, M[Y];	// M[CUR_OLDX] <= X
			LDXI CUR_OLDY;
			POPY;
			STY, M[X];	// M[CUR_OLDY] <= Y
			RTS;
			
NextRow:	CLRX;
NextRow1:	INY;
			CPYI VGA_ROWS;
			IF_Y_GE_THEN ScrollUp;
			JR SetCursorXY;

ScrollUp:	CLRY;
NxtRow:		PUSHY; 		// we will need row later
			CLRX;
			JSR GetRowBase;
NxtCol:		PUSHY;		// destination base on stack
			ADYI VGA_COLS;	// add to get source base
			LDA, M[Y+X];	// get character from VGA RAM
			POPY;			// destination base
			STA, M[Y+X];	// store character to one row below, same column
			INX;			// next column
			CPXI VGA_COLS;
			IF_X_GE_THEN ChkRow;
			JR NxtCol;
ChkRow:		POPY;
			INY;
			CPYI VGA_ROWS - 1;
			IF_Y_GE_THEN ClrLastRow;
			JR NxtRow;
			// clear last row
ClrLastRow:	NOP;
			LDYI VGA_ROWS - 1;
			PUSHY;	
			JSR ClearRow;
			POPY;		// set cursor position to X = 0, Y = ROWS - 1
			JR SetX0;
				
VGA_CR:		POPA;
SetX0:		CLRX;
			JR SetCursorXY;

VGA_BS:		POPA;
			CLRX;
			IF_X_EQ_THEN PrevRow;
			DEX, JR SetCursorXY;
PrevRow:	CHKY;
			IF_Y_EQ_THEN SetCursorXY;
			LDXI VGA_COLS - 1;
			DEY, JR SetCursorXY;
			
VGA_LF:		POPA;
			JR NextRow1;

VGA_CLS:	POPA;
			LDAI ' ';	// use space character to clear screen
			CLRY;
ClrNextRow:	PUSHY; 
			JSR ClearRow;
			POPY;
			INY;
			CPYI VGA_ROWS;
			IF_Y_GE_THEN vga_home1;
			JR ClrNextRow;
			
ClearRow: 	JSR GetRowBase;
			LDAI ' ';
			LDXI VGA_COLS / 4 - 1; // bar on the left just to save time
ClrRLoop:	STA, M[Y+X];
			IF_X_EQ_THEN ClrDone;
			DEX, JR ClrRLoop;
ClrDone:	RTS;			
			
VGA_HOME:	POPA;
vga_home1:	CLRXY; // X, Y <= 0
			JR SetCursorXY;

GetOldCrsXY:LDYI CUR_OLDX;
			LDX, M[Y];		// X <= M[CURSOR_X]
			PUSHX;
			LDXI CUR_OLDY;
			JR GetCursorY;

GetCursorXY:LDYI CURSOR_X;
			LDX, M[Y];		// X <= M[CURSOR_X]
			PUSHX;
			LDXI CURSOR_Y;
GetCursorY:	LDY, M[X];		// Y <= M[CURSOR_Y]
			POPX;
			RTS;	

GetRowBase:	PUSHA;
			ADYI ROW_OFFSET;
			LDA, M[Y];		// row start address in VGA memory
			LDY, A;			// to Y as index base
			POPA;
			RTS;
			
VGA_OutStr:	LDA, M[X];
			IF_AZ;
			.relative OutStrDone;
			JSR VGA_OutChr;
			INX, BRANCH;
			.relative VGA_OutStr;
OutStrDone:	RTS;
