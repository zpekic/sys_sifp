//---------------------------------------------------------------------------------------------
//	Hello world test code for SIFP (c) 2023-... zpekic@hotmail.com
//  Compile with https://github.com/zpekic/MicroCodeCompiler
//---------------------------------------------------------------------------------------------
// Test code to load into internal ROM of SIFP microcomputer
// Full description: 
// https://hackaday.io/project/173996-sifp-single-instruction-format-processor
//---------------------------------------------------------------------------------------------
			.code 12, 16, helloworld_code.mif, helloworld_code.cgf, helloworld_code.coe, cpu:helloworld_code.vhd, helloworld_code.hex, helloworld_code.bin, 2;
			.symbol 12, 256, helloworld_sym.mif, helloworld_sym.cgf, helloworld_sym.coe, sym:helloworld_sym.vhd, helloworld_sym.hex, helloworld_sym.bin, 32;

#include "sifp.mcc";
#include "sifp_sys.mcc";

//	---------------------------------------------------------------------------
			.org 0;
//	Execution starts at location 0
//	---------------------------------------------------------------------------
			INTOFF;		// pro-forma, IE flag is cleared by reset
			TRACEOFF;	// pro-forma, TE flag is cleared by reset
			JUMP;
			.absolute Init;
			
//	---------------------------------------------------------------------------
//	interrupt handling routine
//	---------------------------------------------------------------------------
			.org 8;
IntHandler:	PUSHA;
			PUSHX;
			PUSHY;

			TRACEON;
			
			MARK2;
			BRANCH;
			.relative GetOldCrsXY;
			PUSHY;
			PUSHX;
			MARK2;
			BRANCH;
			.relative GetRowBase;
			LDA, M[Y+X];	// get character from VGA RAM
			ANDI;
			.data 0xFF7F;	// revert to original
			STA, M[Y+X];
			MARK2;
			BRANCH;
			.relative GetCursorXY;
			CPX, M[POP];	// new X - old X
			CPY, M[POP];	// new Y - old Y
			IF_XZ;
			.relative CheckY;
			BRANCH;
			.relative NoFlip; 
CheckY:		IF_YZ;
			.relative Flip;
NoFlip:		MARK2;
			BRANCH;
			.relative SetOldCrsXY;
			BRANCH;
			.relative IntExit;
			
Flip:		MARK2;
			BRANCH;
			.relative GetCursorXY;
			MARK2;
			BRANCH;
			.relative GetRowBase;
			LDA, M[Y+X];	// get character from VGA RAM
			XORI;
			.data 0x0080;	// reverse back and forth
			STA, M[Y+X];
			
IntExit:	POPY;
			POPX;
			POPA;
			POPF;	// F and P were saved when entering the interrupt routine
			RTS;
//	---------------------------------------------------------------------------			

Init:		MARK2;
			BRANCH;
			.relative VGA_Init;

			MARK2;
			BRANCH;
			.relative UART_Init;

			//TRACEON;
			INTON;
PrintLoop:	MARK2;
			BRANCH;
			.relative UART_GetChr;
PrintLoop1:	MARK2;
			BRANCH;
			.relative PrintChr;
			PUSHA;
			XORI;
			.data ETX;
			IF_AZ;
			.relative Continue;
			POPA;
			XORI;
			.data CR;
			IF_AZ;
			.relative PrintLF;
			BRANCH;
			.relative PrintLoop;
PrintLF:	LDAI;
			.data LF;
			BRANCH;
			.relative PrintLoop1; 
			
Continue:	POPA;
			LDXI;
			.data @StrReady;
			MARK2;
			BRANCH;
			.relative PrintChr;

			MARK2;	// set proper cursor X, Y...
			BRANCH;
			.relative GetCursorXY;
			MARK2;
			BRANCH;
			.relative SetOldCrsXY;
			
			LDAI;
			.data 0x89AB;
			MARK2;
			BRANCH;
			.relative PrintHex;
			
			INTON;	// and then enable interrupts
			
// -------------------------------------
// MainLoop START
// -------------------------------------
MainLoop:	LDYI;
			.data TXTBUFFER;
			
			// clear text buffer
			LDX, LDA, M[IMM];
			.data 0;
TxtClear:	STA, M[Y+X];
			CPXI;
			.data TXTBUFLEN;
			IF_XC;
			.relative TxtCleared;
			INX, BRANCH;
			.relative TxtClear;

TxtCleared: LDXI; // X holds position of cursor within the buffer
			.data 0;
			// edit within the text buffer
			MARK2;
			BRANCH;
			.relative CmdEdit;
			IF_AC;
			.relative ErrOrBrk;
			
			// print text buffer
			MARK2;
			BRANCH;
			.relative Print;

			// execute command in text buffer
			MARK2;
			BRANCH;
			.relative CmdExec;
			IF_AC;
			.relative ErrOrBrk;

			// dead loop
			BRANCH;
			.relative MainLoop;
// -------------------------------------
// MainLoop END
// -------------------------------------

CmdExec:	MARK2;
			BRANCH;
			.relative UART_IsBrk;
			IF_AC;
			.relative CmdBreak;
			LDAI;
			.data 0xFFFF;
			RRC;	
CmdBreak:	RTS;	// just return now with an error code
			
ErrOrBrk:	LDYI;
			.data @StrBreak;
			LDXI;
			.data ETX;	
			CPX, A;		// A = CTRLC?
			IF_AZ;		// yes, just print break message
			.relative Print;
			PUSHA;		// save error code
			LDYI;
			.data @StrError;
			MARK2;
			BRANCH;
			.relative Print;
			POPA;		 // restore error code
			MARK2;
			BRANCH;
			.relative PrintHexA;
			LDYI;
			.data @StrEnd;
			BRANCH;
			.relative Print;

CmdEdit:	MARK2;
			BRANCH;
			.relative UART_GetChr;
			PUSHA;

			MARK2;
			BRANCH;
			.relative PrintChr;

			LDA, M[S];
			XORI;
			.data CR;	// Enter? 
			IF_AZ;
			.relative LeaveEdit;
			
			LDA, M[S];
			XORI;
			.data ETX;	// CTRL-C? 
			IF_AZ;
			.relative BrkEdit;
			
			LDA, M[S];
			XORI;
			.data CLS;	// CLS? 
			IF_AZ;
			.relative BrkEdit;

			LDA, M[S];
			XORI;
			.data HOME;	// HOME? 
			IF_AZ;
			.relative BrkEdit;
			
			LDA, M[S];
			XORI;
			.data BS;
			IF_AZ;
			.relative BSEdit;
			
			// "regular character"
			POPA;
			STA, M[Y+X];
			CPXI;
			.data TXTBUFLEN;
			IF_XZ;
			.relative BumpEnd;
			LDAI;
			.data 0;
			STA, M[Y+X];
			INX, BRANCH;
			.relative CmdEdit;

			// break edit mode with AC set
BrkEdit:	POPA;
			LDAI;
			.data 1;
			RRC;
			LDAI;
			.data ETX;	// ETX code denoted break, not error
			RTS;
			
			// break edit mode with AC cleared
LeaveEdit:	POPA;
			LDAI;
			.data 0;
			STA, M[Y+X];
			RRC;
			RTS;
			
			// clear current character and move one left
BSEdit:		POPA;
			LDAI;
			.data 0;
			STA, M[Y+X];
			CPX, A;		// test if X == 0
			IF_XZ;
			.relative BumpStart;
			DEX, BRANCH;
			.relative CmdEdit;

BumpStart:	PUSHX;
			PUSHY;
			LDYI;
			.data @StrBeep;
Bumped:		MARK2;
			BRANCH;
			.relative Print;
			POPY;
			POPX;
			BRANCH;
			.relative CmdEdit;
			
BumpEnd:	PUSHX;
			PUSHY;
			LDYI;
			.data @StrBack;
			BRANCH;
			.relative Bumped;
			
#include "utils.sif";
#include "uart.sif";
#include "vga.sif";		
#include "strings.sif";
	
			
