			.org $;
UART_Init: 	LDYI;
			.data UART_CONTROL;
			LDAI;
			.data 3;	// reset
			STA, M[Y];
			LDAI;
			.data 0x10;	// all int off, 8N2, baudrate / 1
			STA, M[Y];
			RTS;

UART_GetChr:PUSHY;
			LDYI;
			.data UART_STATUS;
RdStatus0:	MARK2;
			BRANCH;
			.into @GetStatus - $;
			ANDI;
			.data 0x0001; // check if RDRF (bit 0, receiver data register full) is 1
			IF_AZ;
			.branchto @RdStatus0 - $;
GetChr:		INY;		// point to UART data register
			LDA, M[Y];
			ANDI;
			.data 0x00FF;	// UART is connected to LSB
			POPY;
			RTS;

UART_IsBrk: PUSHA;
			PUSHY;
			LDYI;
			.data UART_STATUS;
			MARK2;
			BRANCH;
			.into @GetStatus - $;
			ANDI;
			.data 0x0001; // check if RDRF (bit 0, receiver data register full) is 1
			IF_AZ;
			.branchto @NoBrk - $;

			INY;		// point to UART data register
			LDA, M[Y];	// get received character
			XORI;
			.data ETX;	// compare with CTRL/C
			IF_AZ;
			.branchto @Brk - $;

NoBrk:		LDAI;
			.data 0;
			BRANCH;
			.to @BrkExit - $;
Brk:		LDAI;
			.data 1;
			SRC;		// AC carry, indicates break character detection;
BrkExit:	POPY;
			POPA;
			RTS;
			
UART_Beep:	LDAI;
			.data BEL;
UART_OutChr:PUSHY;
			PUSHA;
			LDYI;
			.data UART_STATUS;
RdStatus1:	MARK2;
			BRANCH;
			.into @GetStatus - $;
			ANDI;
			.data 0x0002; // check if TDRE (bit 1, transmit register empty) is 1
			IF_AZ;
			.branchto @RdStatus1 - $;
OutChr:		INY, LDA, M[POP];	// Y points to UART data register, get the char back		
			STA, M[Y];
			POPY;
			RTS;
			
GetStatus:	LDA, M[Y];
			PUSHA;
			LDA, M[Y];
			XOR, M[S];
			IF_AZ;
			.branchto @StIsStable - $;
			POPA;
			BRANCH;
			.to @GetStatus - $;
StIsStable:	POPA;
			RTS;
			
UART_OutStr:LDA, M[X];
			IF_AZ;
			.branchto @UART_Done - $;
			MARK2;
			BRANCH;
			.into @UART_OutChr - $;
			INX, BRANCH;
			.data @UART_OutStr - $;
UART_Done:	RTS;